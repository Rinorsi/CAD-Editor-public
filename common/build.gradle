plugins {
    id "dev.architectury.loom" version "1.10.433"
    id "architectury-plugin"
    id "java"
    id "maven-publish"
}

def mixinVersion = rootProject.findProperty("mixin_version") ?: "0.8.7"
def fabricMixinVersion = rootProject.findProperty("fabric_mixin_version") ?: "0.12.5+mixin.0.8.5"

architectury {
    common("fabric", "neoforge")
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set((rootProject.hasProperty("java_version") ? rootProject.java_version : "21") as Integer)
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-Xlint:all", "-Xlint:-processing"]
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.officialMojangMappings()
    compileOnly "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    compileOnly "org.spongepowered:mixin:${mixinVersion}"
    compileOnly "org.ow2.asm:asm:9.6"
    compileOnly "org.ow2.asm:asm-tree:9.6"
    compileOnly "com.google.errorprone:error_prone_annotations:2.28.0"
    loaderLibraries "net.fabricmc:sponge-mixin:${fabricMixinVersion}"
    annotationProcessor "org.spongepowered:mixin:${mixinVersion}:processor"
    annotationProcessor "com.google.code.gson:gson:2.10.1"
    annotationProcessor "com.google.guava:guava:33.2.1-jre"
}

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
                runtimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.2"
            }
        }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/cadeditor.accesswidener")
}

publishing {
    publications {
        create("mavenCommon", MavenPublication) {
            artifactId = rootProject.archives_base_name
            artifact(tasks.named("remapJar"))
            artifact(tasks.named("remapSourcesJar")) { classifier "sources" }
        }
    }
    repositories { }
}
